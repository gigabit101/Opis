buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "CB Repo"
            url = "http://chickenbones.net/maven"
        }
        jcenter()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'net.covers1624:GradleStuff:1.0-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:3.4.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'net.covers1624.contained-deps'
apply plugin: 'net.covers1624.repacked-deps'
apply plugin: 'de.undercouch.download'
apply plugin: 'maven'

group = "mcp.mobius" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Opis"

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${project.config.mod_version}." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of ${archivesBaseName}. Version: ${version}"
println "Using Forge ${config.forge_version}, for Minecraft ${config.mc_version}"

minecraft {
    version = config.mc_version + "-" + config.forge_version
    mappings = config.mappings
    runDir = "run"
    replace '${mod_version}', project.config.mod_version
    makeObfSourceJar = false
}

containedDeps {
    configuration = "pack"
    tasks = "jar, deobfJar"
}

repackedDeps {
    configuration = "shade"
    tasks = "jar, deobfJar"
    addExtraSrgLine "PK: org/apache/commons/math3 mcp/mobius/opis/repack/org/apache/commons/math3"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace properties in info files
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        expand 'version': project.version, 'mc_version': config.mc_version
    }

    // Move access transformer to META-INF
    rename '(.+_at.cfg)', 'META-INF/$1'
}

configurations {
    pack
    compile.extendsFrom pack
    //TODO, Smart shade.
    compile.extendsFrom shade
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "CB Repo"
        url = "http://chickenbones.net/maven"
    }
}

dependencies {
    compile "codechicken:CodeChickenLib:${config.mc_version}-${config.ccl_version}:deobf"
    pack "codechicken:ChickenASM:1.12-${config.cc_asm_version}"
    pack 'org.jfree:jcommon:1.0.21'
    pack 'org.jfree:jfreechart:1.0.17'
    pack 'com.miglayout:miglayout-swing:4.2'
    pack 'net.sf.opencsv:opencsv:2.3'
    shade 'org.apache.commons:commons-math3:3.2'
}

version = "${project.minecraft.version}-${project.version}"

def commonManifest = {
    attributes 'FMLAT': 'opis_at.cfg'
}

jar {
    classifier = 'universal'
    manifest commonManifest
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task downloadMapwriter(type: Download) {
    src "https://media.forgecdn.net/files/2510/933/MapWriter-1.12.2-2.8.2.jar"
    dest "libs/MapWriter-1.12.2-2.8.2.jar"
    useETag true
    onlyIfModified true
}
mergeJars.dependsOn downloadMapwriter

task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf { // Skip the task if our secret data isn't available
        project.hasProperty('keyStore')
    }

    if (project.hasProperty('keyStore')) {
        keyStore = project.keyStore // This needs to be a path to the keystore file
        alias = project.keyStoreAlias
        storePass = project.keyStorePass
        keyPass = project.keyStoreKeyPass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar

// Tell the artifact system about our extra jars
artifacts {
    archives deobfJar
    archives srcJar
}

// Configure an upload task.
uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven") || project.hasProperty("maven_pass")) {
                if (project.hasProperty("maven_pass")) {
                    println "Uploading to remote maven."
                    repository(url: "http://maven_upload.covers1624.net/") {
                        authentication(userName: "covers1624", password: project.getProperty("maven_pass"))
                    }
                } else {
                    println "Uploading to local maven."
                    repository(url: "file://${local_maven}")
                }
                pom {
                    groupId = project.group
                    // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Opis'
                        url 'https://github.com/Gigabit101/Opis'

                        scm {
                            url 'https://github.com/Gigabit101/Opis'
                            connection 'scm:git:git://github.com/Gigabit101/Opis.git'
                            developerConnection 'scm:git:git@github.com:Gigabit101/Opis.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/Gigabit101/Opis/issues'
                        }

                        licenses {
                            license {
                                name 'GNU Lesser Public License (GPL), Version 2.1'
                                url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'Gigabit101'
                                name 'Gigabit101'
                                roles { role 'developer' }
                            }
                            developer {
                                id 'cover1624'
                                name 'covers1624'
                                roles { role 'developer' }
                            }
                            developer {
                                id 'ProfMobius'
                                name 'ProfMobius'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}
